import static se.bjurr.gitchangelog.api.GitChangelogApi.gitChangelogApiBuilder
import static se.bjurr.gitchangelog.api.GitChangelogApiConstants.REF_MASTER

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'co.riiid:gradle-github-plugin:0.4.2'
        classpath 'gradle.plugin.se.bjurr.gitchangelog:git-changelog-gradle-plugin:1.54'
    }
}

// Release our assets to Github
apply plugin: 'co.riiid.gradle'
apply plugin: 'se.bjurr.gitchangelog.git-changelog-gradle-plugin'

if (deps.build.ci) {
    github {
        owner = 'SodaLabs'
        repo = 'Sparkpoint'
        token = System.getenv('GITHUB_API_TOKEN')
        tagName = 'v' + artifact_version
        targetCommitish = 'master'
        name = 'Sparkpoint Release Version ' + artifact_version
        body = gitChangelogApiBuilder()
                .withGitHubToken(System.getenv('GITHUB_API_TOKEN'))
                .withFromRef('git describe --abbrev=0 --tags'.execute().text.trim())
                .withToRef(REF_MASTER)
                .withTemplatePath("releasenotes.mustache")
                .withUntaggedName("Version " + artifact_version)
                .withNoIssueName("Other issues")
                .render()
        assets = [
                "app/build/outputs/apk/debug/Sparkpoint-debug-${artifact_version}.apk",
                "app/build/outputs/apk/release/Sparkpoint-release-${artifact_version}.apk",
                "app/build/outputs/mapping/release/Sparkpoint-release-${artifact_version}-mapping.txt"
        ]
    }
}